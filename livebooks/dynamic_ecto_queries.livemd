# Elixir

```elixir
Mix.install([
  {:ecto_sql, "~> 3.9"},
  {:ecto, "~> 3.9"},
  {:postgrex, "~> 0.16.5"},
  {:phoenix_html, "~> 3.3"},
  {:phoenix, "~> 1.7"},
  {:phoenix_live_view, "~> 0.18.17"},
  {:kino_db, "~> 0.2.1"},
  {:exqlite, "~> 0.11.0"},
  {:ecto_sqlite3, "~> 0.9.1"}
])
```
## Try it yourself!

[![Run in Livebook](https://livebook.dev/badge/v1/black.svg)](https://livebook.dev/run?url=https%3A%2F%2Fgithub.com%2Finspired-consulting%2FElixir-Meetup-Cologne%2Fblob%2Fmain%2Flivebooks%2Fdynamic_ecto_queries.livemd)

## Some Use Case

```elixir
defmodule User do
  use Ecto.Schema
  @primary_key false
  schema "users" do
    field(:name, :string)
    field(:age, :integer)
  end
end

defmodule Repo do
  use Ecto.Repo, adapter: Ecto.Adapters.SQLite3, otp_app: :nothing
end

case Ecto.Repo.all_running() do
  [_ | _] -> Repo.stop()
  _ -> []
end

Repo.start_link(database: "anti-aging.db")

[
  "DROP TABLE IF EXISTS users",
  "CREATE TABLE users (name STRING, age INT)"
]
|> Enum.map(&Ecto.Adapters.SQL.query!(Repo, &1, []))

users = [
  %{name: "Nisha", age: 21},
  %{name: "Hans", age: 12},
  %{name: "Ariadne", age: 23},
  %{name: "JosÃ©", age: 42},
  %{name: "Frank", age: 51}
]

Repo.insert_all("users", users, [])
```

```elixir
import Ecto.Query
max_age = 50
min_age = 12
query = from(u in User, where: u.age > ^min_age and u.age < ^max_age)
Repo.all(query) |> Kino.DataTable.new()
```

```elixir
defmodule BadExample do
  import Ecto.Query

  def age(min_age, max_age) do
    query = from(u in User, where: u.age > ^min_age and u.age < ^max_age)
    Repo.all(query) |> Kino.DataTable.new()
  end
end

BadExample.age(nil, 23) |> Kino.DataTable.new()
```

```elixir
defmodule UglyExample do
  import Ecto.Query

  def age(min_age, max_age) when is_nil(min_age) and is_nil(max_age) do
    query = from(u in User)
    Repo.all(query)
  end

  def age(min_age, max_age) when is_nil(min_age) do
    query = from(u in User, where: u.age < ^max_age)
    Repo.all(query)
  end

  def age(min_age, max_age) when is_nil(max_age) do
    query = from(u in User, where: u.age > ^min_age)
    Repo.all(query)
  end

  def age(min_age, max_age) do
    query = from(u in User, where: u.age < ^max_age and u.age > ^min_age)
    Repo.all(query)
  end
end

UglyExample.age(nil, 23) |> Kino.DataTable.new()
```

```elixir
defmodule GoodExample do
  import Ecto.Query

  def age(min_age, max_age) do
    query = from(u in User)

    query
    |> maybe_add_min_age(min_age)
    |> maybe_add_max_age(max_age)
    |> Repo.all()
  end

  defp maybe_add_min_age(query, nil), do: query
  defp maybe_add_min_age(query, min_age), do: where(query, [u], u.age >= ^min_age)

  defp maybe_add_max_age(query, nil), do: query
  defp maybe_add_max_age(query, max_age), do: where(query, [u], u.age <= ^max_age)
end

GoodExample.age(nil, 23) |> Kino.DataTable.new()
```
